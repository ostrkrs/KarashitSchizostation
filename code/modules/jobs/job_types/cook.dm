/datum/job/cook
	title = JOB_COOK
	alt_titles = JOB_COOK_ALT_TITLES
	description = "Serve food, cook meat, keep the crew fed."
	department_head = list(JOB_HEAD_OF_PERSONNEL)
	faction = FACTION_SHIP
	total_positions = 2
	spawn_positions = 2
	supervisors = SUPERVISOR_STEWARD
	exp_granted_type = EXP_TYPE_CREW
	config_tag = "COOK"

	outfit = /datum/outfit/job/cook

	paycheck = PAYCHECK_CREW
	paycheck_department = ACCOUNT_SRV

	mind_traits = list(TRAIT_DESENSITIZED) // butcher
	liver_traits = list(TRAIT_CULINARY_METABOLISM)

	display_order = JOB_DISPLAY_ORDER_COOK
	bounty_types = CIV_JOB_COOK
	departments_list = list(
		/datum/job_department/service,
		)

	family_heirlooms = list(
		/obj/item/reagent_containers/condiment/saltshaker,
		/obj/item/kitchen/rollingpin,
		/obj/item/clothing/head/utility/chefhat,
	)

	// Adds up to 100, don't mess it up
	mail_goodies = list(
		/obj/item/storage/box/ingredients/random = 40,
		/obj/item/reagent_containers/cup/bottle/caramel = 7,
		/obj/item/reagent_containers/condiment/flour = 7,
		/obj/item/reagent_containers/condiment/rice = 7,
		/obj/item/reagent_containers/condiment/ketchup = 7,
		/obj/item/reagent_containers/condiment/enzyme = 7,
		/obj/item/reagent_containers/condiment/soymilk = 7,
		/obj/item/kitchen/spoon/soup_ladle = 6,
		/obj/item/kitchen/tongs = 6,
		/obj/item/knife/kitchen = 4,
		/obj/item/knife/butcher = 2,
	)

	rpg_title = "Tavern Chef"
	job_flags = SHIP_JOB_FLAGS

/datum/job/cook/award_service(client/winner, award)
	winner.give_award(award, winner.mob)

	var/datum/venue/restaurant = SSrestaurant.all_venues[/datum/venue/restaurant]
	var/award_score = restaurant.total_income
	var/award_status = winner.get_award_status(/datum/award/score/chef_tourist_score)
	if(award_score > award_status)
		award_score -= award_status
	winner.give_award(/datum/award/score/chef_tourist_score, winner.mob, award_score)


/datum/outfit/job/cook
	name = "Cook"
	jobtype = /datum/job/cook

	id_trim = /datum/id_trim/job/cook
	uniform = /obj/item/clothing/under/costume/buttondown/slacks/service
	suit = /obj/item/clothing/suit/toggle/chef
	backpack_contents = list(/obj/item/sharpener = 1,)
	belt = /obj/item/modular_computer/pda/cook
	ears = /obj/item/radio/headset/headset_srv
	head = /obj/item/clothing/head/utility/chefhat

	var/datum/martial_art/cqc/under_siege/style

/datum/outfit/job/cook/pre_equip(mob/living/carbon/human/H, visuals_only = FALSE)
	..()
	style = new(src)
	style.refresh_valid_areas()

/datum/outfit/job/cook/post_equip(mob/living/carbon/human/user, visuals_only = FALSE)
	..()
	style.teach(user)
